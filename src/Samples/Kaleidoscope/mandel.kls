# unary operator to perform a logical NOT
def unary!(v)
  if v then
    0
  else
    1;

# unary operator to perform a mathematical negation
def unary-(v)
  0-v;

# Comparison operator
def binary> 10 (LHS RHS)
  RHS < LHS;

# Logical OR operator
def binary| 5 (LHS RHS)
  if LHS then
    1
  else if RHS then
    1
  else
    0;

# Logical AND operator
def binary& 6 (LHS RHS)
  if !LHS then
    0
  else
    !!RHS;

# arithmatic equality test
def binary== 9 (LHS RHS)
  !(LHS < RHS | LHS > RHS);

# selector operator
def binary : 1 (x y) y;

# declare access to built-in function
extern putchard(char);

def printdensity(d)
  if d > 8 then
    putchard(32)     # 32 => ASCII Space
  else if d > 4 then
    putchard(46)     # 46 => ASCII '.'
  else if d > 2 then
    putchard(43)     # 43 => ASCII '+'
  else
    putchard(42);    # 43 => ASCII '*'

def mandelconverger(real imag iters creal cimag)
  if iters > 255 | (real*real + imag*imag > 4) then
    iters
  else
    mandelconverger( real*real - imag*imag + creal
                   , 2*real*imag + cimag
                   , iters+1
                   , creal
                   , cimag
                   );

def mandelconverge(real imag)
  mandelconverger(real, imag, 0, real, imag);

def mandelhelp(xmin xmax xstep   ymin ymax ystep)
  for y = ymin, y < ymax, ystep in
  (
    (for x = xmin, x < xmax, xstep in printdensity(mandelconverge(x,y))) : putchard(10)
  );

def mandel(realstart imagstart realmag imagmag)
  mandelhelp(realstart, realstart+realmag*78, realmag, imagstart, imagstart+imagmag*40, imagmag);

mandel(-2.3, -1.3, 0.05, 0.07);
